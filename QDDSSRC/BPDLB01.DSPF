      *-----------------------------------------------------------------------*
      * To create the BPDLB01 display file:                                   *
      * CRTDSPF FILE(BPLIB/BPDLB01) SRCFILE(BPLIB/QDDSSRC) REPLACE(*YES)      *
      *-----------------------------------------------------------------------*
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A          R SFL01                     SFL
     A*%%TS  SD  20180215  103743  DICKJAZ     REL-V7R2M0  5770-WDS
     A            XRRNS1         3S 0H
     A            SF1H01    R        H      REFFLD(POPUOM/PUOMINDEX *LIBL/POPUO-
     A                                      M)
     A            SF1SEL         1A  B  3  3DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A                                      VALUES(' ' '1')
     A            SF1UOM    R        O  3  6REFFLD(POPUOM/UOMID *LIBL/POPUOM)
     A                                      DSPATR(HI)
     A            SF1DSC    R        O  3 11REFFLD(POPUOM/UOMDESC *LIBL/POPUOM)
     A                                      DSPATR(HI)
     A          R CTL01                     SFLCTL(SFL01)
     A*%%TS  SD  20180215  104013  DICKJAZ     REL-V7R2M0  5770-WDS
     A                                      CA12(12 'Previous')
     A                                      ROLLDOWN(25 'ROLL DOWN')
     A                                      ROLLUP(26 'ROLL UP')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&RELREC)
     A  27                                  SFLDSP
     A  28                                  SFLDSPCTL
     A  29                                  SFLCLR
     A  30                                  SFLEND(*MORE)
     A  *DS3                                SFLSIZ(0006)
     A  *DS4                                SFLSIZ(0006)
     A  *DS3                                SFLPAG(0005)
     A  *DS4                                SFLPAG(0005)
     A  *DS3                                WINDOW(WIN01)
     A  *DS4                                WINDOW(WIN01)
     A            RELREC         5S 0H
     A                                  2  3'1'
     A                                  2 11'Description'
     A                                  1  1'List Box Heading Goes Here '
     A                                      DSPATR(HI)
     A                                  2  5'Code'
     A          R WIN01
     A*%%TS  SD  20180215  103743  DICKJAZ     REL-V7R2M0  5770-WDS
     A  *DS3                                WINDOW(9 24 9 27 *NOMSGLIN)
     A  *DS4                                WINDOW(9 24 9 27 *NOMSGLIN)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  9  1'F12=Previous'
     A                                      COLOR(BLU)
     A          R DUMMY                     ASSUME
     A                                  1  2' '
